#version 450

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(binding = 0, rgba16f) uniform image2D accumColor;
layout(binding = 1) uniform sampler2D accumWeight;
layout(binding = 2) uniform sampler2D depthSampler;

void main()
{
    ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(accumColor);
    if (coord.x >= size.x || coord.y >= size.y)
        return;

    vec4 color = imageLoad(accumColor, coord);
    vec2 weightDepth = texelFetch(accumWeight, coord, 0).rg;
    float weight = weightDepth.x;

    if (weight <= 1e-5)
    {
        imageStore(accumColor, coord, vec4(0.0));
        return;
    }

    float avgDepth = weightDepth.y / weight;
    float depthRef = texelFetch(depthSampler, coord, 0).r;

    float depthDiff = abs(avgDepth - depthRef);
    float depthSigma = 0.01;
    float depthFactor = exp(-(depthDiff * depthDiff) / (2.0 * depthSigma * depthSigma));

    vec3 resolved = color.rgb / weight;
    resolved *= depthFactor;

    imageStore(accumColor, coord, vec4(resolved, 1.0));
}
