#version 450

layout (local_size_x = 16, local_size_y = 16) in;

layout(set = 0, binding = 0, r16f) uniform image2D aoInput;
layout(set = 0, binding = 1, r16f) uniform image2D aoOutput;
layout(set = 1, binding = 2) buffer correctedDepth
{
    float depth[];
};

// Depth convention: valid pixels have depth > 0, increasing with distance.
layout(push_constant) uniform PC {
    layout(offset = 0) ivec2 screenSize;
    layout(offset = 8) int horizontal;
    layout(offset = 12) float depthThreshold;
    layout(offset = 16) float sigma;
} pc;

float fetchDepth(ivec2 coord)
{
    ivec2 clamped = clamp(coord, ivec2(0), pc.screenSize - ivec2(1));
    uint idx = uint(clamped.x + clamped.y * pc.screenSize.x);
    return depth[idx];
}

float gaussianWeight(int offset)
{
    float x = float(offset);
    float variance = 2.0 * pc.sigma * pc.sigma;
    return exp(-(x * x) / variance);
}

void main()
{
    ivec2 gid = ivec2(gl_GlobalInvocationID.xy);
    if (gid.x >= pc.screenSize.x || gid.y >= pc.screenSize.y)
        return;

    uint idx = uint(gid.x + gid.y * pc.screenSize.x);
    float centerDepth = depth[idx];
    float centerAO = imageLoad(aoInput, gid).r;

    if (centerDepth <= 0.0f)
    {
        imageStore(aoOutput, gid, vec4(1.0, 0.0, 0.0, 0.0));
        return;
    }

    float totalWeight = 0.0;
    float aoAcc = 0.0;

    const int radius = 2;
    for (int i = -radius; i <= radius; ++i)
    {
        ivec2 offset = pc.horizontal != 0 ? ivec2(i, 0) : ivec2(0, i);
        ivec2 sampleCoord = gid + offset;

        float sampleDepth = fetchDepth(sampleCoord);
        if (sampleDepth <= 0.0f)
            continue;

        float norm = max(max(centerDepth, sampleDepth), 1e-6);
        float rel = abs(sampleDepth - centerDepth) / norm;
        if (rel > pc.depthThreshold)
            continue;

        float depthWeight = exp(-rel * 6.5);
        float w = gaussianWeight(i) * depthWeight;

        float sampleAO = imageLoad(aoInput, sampleCoord).r;
        totalWeight += w;
        aoAcc += sampleAO * w;
    }

    float filtered = (totalWeight > 0.0) ? (aoAcc / totalWeight) : centerAO;
    imageStore(aoOutput, gid, vec4(filtered, 0.0, 0.0, 0.0));
}
